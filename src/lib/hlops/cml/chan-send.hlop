(* chan-send.hlop
 *
 * COPYRIGHT (c) 2007 The Manticore Project (http://manticore.cs.uchicago.edu)
 * All rights reserved.
 *
 * Send a message on a channel.
 *)

#include "events.def"
#include "assert.def"

define @chan-acquire-lock (ch : chan / _ : exh) : ();
define @chan-release-lock (ch : chan / _ : exh) : ();
define @event-claim (flg : dirty_flag / exh : exh) : bool;
define @atomic-enqueue (fgs : fgs, fiber : fiber / exh : exh) : ();
define @chan-dequeue-recv (ch : chan / exh : exh) : option;
define @chan-enqueue-send (ch : chan, msg : any, fgs : fgs, k : cont(unit) / _ : exh) : ();
define @get-fgs (vp : vproc / exh : exh) : fgs;
define @throw-to (vp : vproc, fgs : fgs, recv : cont(any), v : any / exh : exh) noreturn;

define @chan-send (arg : [chan, any] / exh : exh) : any =
    let ch : chan = #0(arg)
    let msg : any = #1(arg)
    let fgs : fgs = @get-fgs(host_vproc / exh)
    do @chan-acquire-lock (ch / exh)
    cont sendK (x : unit) = return (x)
    (* in *)
      fun tryLp (_ : unit / exh : exh) : any =
	    let maybeItem : option = @chan-dequeue-recv(ch / exh)
	    (* in *)
	      case maybeItem
	       of SOME(item : recvq_item) =>
		  (* there is a matching recv, but we must check to make sure
		   * that some other thread has not already claimed the event.
		   *)
		    let success : bool = @event-claim (#0(item) / exh)
		    (* in *)
		      if success then (* we got it *)
			do @chan-release-lock(ch / exh)
			do @atomic-enqueue (fgs, sendK / exh)
			(* in *)
			  @throw-to (host_vproc, #1(item), #2(item), msg / exh)
		      else (* someone else got the event, so try again *)
			apply tryLp (UNIT / exh)
		| NONE =>
		    do @chan-enqueue-send (ch, msg, fgs, sendK / exh)
		    do @chan-release-lock (ch / exh)
		    (* in *)
		      @thread-exit(/exh)
	      end
      (* in *)
	apply tryLp (UNIT / exh)
;
