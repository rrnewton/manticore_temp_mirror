(* ltc-ivar-get.hlop
 *
 * COPYRIGHT (c) 2007 The Manticore Project (http://manticore.cs.uchicago.edu)
 * All rights reserved.
 *
 * get function for i-variable.
 *)

#include "ivar.def"
#include "debugging.def"
#include "fiber-cancellation.def"

define @ltc-ivar-get (ivar : ivar, cc : cancel_cell / exh : exh) : any =
    let readFlag : int = I32FetchAndAdd(&2(ivar), 1)
    let value : any = #1(ivar)
    if Equal(value, IVAR_EMPTY_VAL)
       then
          (* atomic update, kinda like fetch and increment *)
          cont k (x : any) = 
              cont k (_ : unit) =
                   return (x)

              (* cancelation *)
              fun cleanupFun () : () = 
                  return ()
               let k : fiber = @cancelable-fiber(cc, k, cleanupFun / exh)
               throw k(UNIT)

          let fgs : fgs = @get-fgs(host_vproc / exh)

          let blockedThread : ivar_blocked_thread = alloc (fgs, k)
          (* atomic update *)
          fun loop () : () = 
             let blocks : list = #0(ivar)
             let newBlocks : list = CONS(blockedThread, blocks)
             let newBlocks : list = promote(newBlocks)
             let blocked : any = CAS(&0(ivar), blocks, newBlocks)
             if Equal(blocked, blocks)
               then return () 
               else apply loop ()
          do apply loop()
          let _ : int = I32FetchAndAdd(&2(ivar), -1)
          @thread-exit(/exh)
        else
          let _ : int = I32FetchAndAdd(&2(ivar), -1)
          return (value)
;
