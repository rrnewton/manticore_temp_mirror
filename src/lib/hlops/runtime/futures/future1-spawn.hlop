(* future1-spawn.hlop
 * 
 * COPYRIGHT (c) 2007 The Manticore Project (http://manticore.cs.uchicago.edu)
 * All rights reserved.
 *
 * Spawn a one-toucher future.
 *)

#include "types.def"
#include "futures.def"
#include "runtime-offsets.def"
#include "tags.def"
#include "fgs.def"
#include "logging.def"

define @future1-work-sharing ( / exh : exh) : locked_queue;

define @future1-spawn (thunk : thunk / exh : exh) : future =  
  (* initialize the futures scheduler and return its spawn function *)
  fun init (_ : unit / exh : exh) : any =
      let futuresQ : locked_queue = @future1-work-sharing ( / exh)
      return (futuresQ)
  let fgs : fgs = @get-fgs (host_vproc / exh)
  (* obtain the spawn function *)
  let futuresQ : locked_queue = @assoc-list-lookup (TAG_SPAWN_FUTURE, init, fgs / exh)
  (* allocate the future1 cell
     a future1 cell consists of two words:
       1) a _state_ word, with one of the following values:
            EMPTY_F
            STOLEN_F
            EVAL_F
            FULL      value
            WAITING   cont
       2) a _thunk_ word 
   *)

  let cancelCell : cancel_cell = @cancel-cell(/exh)
  let fut : future = alloc (EMPTY_F, thunk, cancelCell)
  let fut : future = promote (fut)

 (* add the future to the scheduling queue *)
  fun wrapper (_ : unit / exh : exh) : unit =      
      do @future1-steal (futuresQ, fut / exh)
      return (UNIT)
  let k : fiber = @fiber (wrapper / exh)
  fun cleanupFun (/ exh : exh) : () = return()
  let k : fiber = @cancellable-fiber(cancelCell, fgs, k, cleanupFun / exh)
  do @locked-queue-enqueue (futuresQ, k / exh)

  do LOG_PTR(RTFuture1SpawnEvt, fut)

  return (fut) 
;
