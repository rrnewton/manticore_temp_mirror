(* locked-queue-enqueue.hlop
 *
 * COPYRIGHT (c) 2007 The Manticore Project (http://manticore.cs.uchicago.edu)
 * All rights reserved.
 *
 * Enqueue an element.
 *)

#include "locked-queue.def"
#include "locked-queue-local.def"

#include "assert.def"
#include "vproc-queue.def"

define @locked-queue-enqueue (q : locked_queue, elt : any / exh : exh) : () =

 (* promote elt *before* acquiring the lock *)
  let qElt : in_place_queue_elt = LOCKED_QUEUE_NEW_ELT(elt)
  let qElt : in_place_queue_elt = promote (qElt)  

  cont noneB () = 
       (* "impossible" *)
       let _ : unit = @spin-unlock-lq (q / exh)
       do assert (FALSE)
       return ()
  cont someB (blockedThread : any) =
       let _ : unit = @spin-unlock-lq (q / exh)
       let blockedThread : blocked_thread = (blocked_thread)blockedThread
       let blockedK : cont(any) = SELECT(BLOCKED_THREAD_CONT, blockedThread)
       let blockedFgs : fgs = SELECT(BLOCKED_THREAD_FGS, blockedThread)
       cont unblockK (x : unit) =
            throw blockedK (elt)
       let unblockK : fiber = (fiber)unblockK
        @atomic-enqueue (blockedFgs, unblockK / exh)

  let _ : unit = @spin-lock-lq (q / exh)

 (* check for blocked threads first *)
  let bqHd : in_place_queue_elt = SELECT(LOCKED_QUEUE_BLOCKED_HD, q)
  do if Equal (bqHd, NIL)
        then (* nothing is blocked; enqueue the element *)
             @in-place-enqueue-lq (q, qElt / exh)
        else (* unblock the thread and pass it the element *)
             do @in-place-dequeue-bq (q, noneB, someB / exh)
             assert(FALSE)

  let _ : unit = @spin-unlock-lq (q / exh)

  return ()
;
